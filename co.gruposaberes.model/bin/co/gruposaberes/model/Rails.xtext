grammar co.gruposaberes.model.Rails hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate rails "http://www.gruposaberes.co/model/Rails"

Class returns RubyClass:
  CLASS_WORD SPACE* name=NamespacedModuleName (SPACE* '<' SPACE* superType=NamespacedModuleName)? SPACE*
    classElements+=ClassElement*
  END_WORD
;

NamespacedModuleName :
(ID'::')*ID
;

MethodChainCall:
	//SPACE* Causes alternatives warning 
	NamespacedModuleName('.'ID)* =>SPACE* PARENTHESIS_BLOCK? => SPACE* BRACKET_BLOCK?
;


ClassElement returns ClassElement:
  Method|Relationship
;

Relationship returns Relationship:
  HasMany | HasAndBelongsToMany | BelongsTo 
;

HasMany returns HasMany:
  HAS_MANY_WORD SPACE* name=(SYMBOL|STRING) SPACE* options+=HashKeyValue* SL_COMMENT?
;

HasAndBelongsToMany returns HasAndBelongsToMany:
  HAS_AND_BELONGS_TO_MANY_WORD SPACE* name=(SYMBOL|STRING) SPACE* options+=HashKeyValue*
;

BelongsTo returns BelongsTo:
  BELONGS_TO_WORD SPACE* name=(SYMBOL|STRING) SPACE* options+=HashKeyValue*
;

HashKeyValue:
  ',' SPACE* (key=ID':'|key=(SYMBOL|STRING) SPACE*'=>') SPACE* value=(SYMBOL|STRING|BRACKET_BLOCK|ARRAY_BLOCK|MethodChainCall) SPACE*
;

Method returns RubyMethod:
  DEF_WORD SPACE* name=MethodName SPACE* PARENTHESIS_BLOCK? => UNTIL_END
;

MethodName:
ID('?'|'=')?
;

terminal SPACE: ' ';

terminal UNTIL_END: -> END_WORD;

terminal BRACKET_BLOCK: '{'->'}';

terminal ARRAY_BLOCK: '['->']';

terminal PARENTHESIS_BLOCK: '('->')';

terminal BELONGS_TO_WORD: 'belongs_to';

terminal HAS_MANY_WORD: 'has_many';

terminal HAS_AND_BELONGS_TO_MANY_WORD: 'has_and_belongs_to_many';

terminal CLASS_WORD: 'class';

terminal MODULE_WORD: 'module';

terminal DEF_WORD: 'def';

terminal END_WORD: 'end';

terminal SYMBOL:':'ID;

terminal ID: ('a'..'z'|'A'..'Z'|'_')('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal STRING: '"'->'"' | "'"->"'";

terminal ML_COMMENT	: '=begin' -> '=end';

terminal SL_COMMENT : '#'->'\n';

terminal WS	: (' '|'\t'|'\r'|'\n')+;

terminal INT returns ecore::EInt: ('0'..'9')+;

terminal ANY_OTHER: .;
